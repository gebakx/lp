class: center, middle

### Llenguatges de Programaci√≥

## Sessi√≥ 5: m√≤nades

![:scale 40%](figures/haskell.png)<br><br>

**Jordi Petit**

![:scale 75%](figures/fib.png)

---
class: left, middle, inverse

## Contingut

- .cyan[Functors]

- Aplicatius

- M√≤nades

- Entrada/sortida

- Exercicis

---

# Functors

.cols5050[
.col1[
Ja sabem aplicar funcions:

```haskell
Œª> (+3) 2                   üëâ  5
```
]
.col2[
Per√≤...

```haskell
Œª> (+3) (Just 2)            ‚ùå
```
]
]
En aquest cas, podem fer servir `fmap`!

```haskell
Œª> fmap (+3) (Just 2)       üëâ  Just 5
Œª> fmap (+3) Nothing        üëâ  Nothing
```

I tamb√© funciona amb `Either`, llistes, tuples i funcions:

```haskell
Œª> fmap (+3) (Right 2)      üëâ  Right 5
Œª> fmap (+3) (Left "err")   üëâ  Left "err"

Œª> fmap (+3) [1, 2, 3]      üëâ  [4, 5, 6]        -- igual que map

Œª> fmap (+3) (1, 2)         üëâ  (1, 5)           -- perqu√® (,) √©s un tipus

Œª> (fmap (*2) (+1)) 3       üëâ  8                -- igual que (.)
```

---

# Functors

`fmap` aplica una funci√≥ als elements d'un contenidor
gen√®ric  `f a` retornant un contenidor del mateix tipus.

`fmap` √©s una funci√≥ de les inst√†ncies de la classe `Functor`:

```haskell
Œª> :type fmap
fmap :: Functor f => (a -> b) -> (f a -> f b)
```

On

```haskell
Œª> :info Functor
class Functor f where
    fmap :: (a -> b) -> (f a -> f b)
```

---

#  `Maybe` √©s functor

El tipus `Maybe` √©s inst√†ncia de `Functor`:

```haskell
Œª> :info Maybe
data Maybe a = Nothing | Just a
instance Ord a => Ord (Maybe a)
instance Eq a => Eq (Maybe a)
instance Applicative Maybe
*instance Functor Maybe
instance Monad Maybe
‚ãÆ
```

Concretament,

```haskell
instance Functor Maybe where
    fmap f Nothing = Nothing
    fmap f (Just x) = Just (f x)
```


---

# Aplicaci√≥

Consulta a una BD:

- Llenguatge sense `Maybe`:

    ```python
    post = Posts.find(1234)
    if post is None:
        return None
    else:
        return post.title
    ```

- En Haskell:

    ```haskell
    fmap getPostTitle (findPost 1234)
    ```

    o tamb√©:

    ```haskell
    getPostTitle `fmap` findPost 1234
    ```

    o millor (`<$>` √©s l'operador infix per a `fmap`):
.xs[(es llegeix *fmap*)]

    ```haskell
    getPostTitle <$> findPost 1234
    ```

---

#  `Either a` √©s functor

El tipus `Either a` √©s inst√†ncia de `Functor`:


```haskell
instance Functor (Either a) where
    fmap f (Left  x) = Left x
    fmap f (Right x) = Right (f x)
```

Fixeu-vos que `Either` t√© dos par√†metres:
- el tipus `Either a` √©s la inst√†ncia de `Functor`,
- el tipus `Either` no.


---

# Les llistes s√≥n functors

El tipus `[]` (llista) √©s inst√†ncia de `Functor`:


```haskell
instance Functor [] where
    fmap = map                  -- potser √©s al rev√©s, poc importa
```


---

# Les funcions s√≥n functors

Les funcions tamb√© s√≥n inst√†ncies de `Functor`:

```haskell
instance Functor ((->) r) where
    fmap = (.)
```

Exemple:

```haskell
Œª> (*3) <$> (+2) <$> Just 1      üëâ   Just 9
Œª> (*3) <$> (+2) <$> Nothing     üëâ   Nothing
```


---

# Lleis dels functors

Les inst√†ncies de functors han de tenir aquestes propietats:

1. Identitat: `fmap id ‚â° id`.

2. Composici√≥: `fmap (g1 . g2) ‚â° fmap g1 . fmap g2`.

<!--
De fet, es pot demostrar que nom√©s existeix una √∫nica possible
inst√†ncia de `Functor` que compleixi aquestes lleis.
-->

<br>
**Nota:**
Haskell no verifica aquestes propietats (per√≤ les pot utilitzar), √©s
responsabilitat del programador fer-ho.

<br>
**Exercici:**
Comproveu que `Maybe`, `Either a`, `[]`, `(,)`
i `(->)` compleixen les lleis dels functors.


---

# Arbres binaris com a functors

Instanciaci√≥ pr√≤pia dels functors pels arbres binaris:

```haskell
data Arbin a
    = Buit
    | Node a (Arbin a) (Arbin a)
    deriving (Show)
```

```haskell
instance Functor (Arbin) where

    fmap f Buit = Buit
    fmap f (Node x fe fd) = Node (f x) (fmap f fe) (fmap f fd)
```

```haskell
a = Node 3 Buit (Node 2 (Node 1 Buit Buit) (Node 1 Buit Buit))

Œª> fmap (*2) a
üëâ Node 6 Buit (Node 4 (Node 2 Buit Buit) (Node 2 Buit Buit))

Œª> fmap even a
üëâ Node False Buit (Node True (Node False Buit Buit) (Node False Buit Buit))
```

<br>
**Exercici:**
Comproveu que `Arbin` compleix les lleis dels functors.

---

# Sumari

La classe `Functor` captura la idea de tipus contenidor  gen√®ric al qual es
pot aplicar una funci√≥ als seus elements per canviar el seu contingut (per√≤ no
el contenidor).

.cols5050[
.col1[
.center[
![:scale 90%](figures/haskell/fmap_apply.png)
![:scale 90%](figures/haskell/fmap_list.png)
]]
.col2[
.center[
![:scale 90%](figures/haskell/fmap_nothing.png)
![:scale 90%](figures/haskell/fmap_function.png)
]]]


Dibuixos: [adit.io](http://adit.io/posts/2013-04-17-functors,_applicatives,_and_monads_in_pictures.html)

---
class: left, middle, inverse

## Contingut

- .brown[Functors]

- .cyan[Aplicatius]

- M√≤nades

- Entrada/sortida

- Exercicis

---

# Aplicatius

.cols5050[
.col1[
Ja sabem aplicar funcions:

```haskell
Œª> (+3) 2                   üëâ  5
```
]
.col2[
I ho sabem fer sobre contenidors:

```haskell
Œª> fmap (+3) (Just 2)       üëâ  Just 5
```
]
]
Per√≤ qu√® passa si la funci√≥ √©s en un contenidor?

```haskell
Œª> (Just (+3)) (Just 2)     ‚ùå
```

En aquest cas, podem fer servir `<*>`! .xs[(es llegeix *app*)]

```haskell
Œª> Just (+3) <*> Just 2             üëâ   Just 5
Œª> Just (+3) <*> Nothing            üëâ   Nothing
Œª> Nothing <*> Just (+3)            üëâ   Nothing
Œª> Nothing <*> Nothing              üëâ   Nothing

Œª> Right (+3) <*> Right 2           üëâ   Right 5
Œª> Right (+3) <*> Left "err"        üëâ   Left "err"
Œª> Left "err" <*> Right 2           üëâ   Left "err"
Œª> Left "err1" <*> Left "err2"      üëâ   Left "err1 "

Œª> [(*2), (+2)] <*> [1, 2, 3]       üëâ   [2, 4, 6, 3, 4, 5]
```

---

# Aplicatius

L'operador `<*>` √©s una operaci√≥ de la classe `Applicative` (que tamb√© ha de ser functor):

```haskell
class Functor f => Applicative f where

    (<*>) :: f (a -> b) -> (f a -> f b)

    pure  :: a -> f a

```

- `<*>` aplica una funci√≥ dins d'un contenidor a uns valors
dins d'un contenidor. Els contenidors s√≥n gen√®rics i del mateix tipus.


- `pure` construeix un contenidor amb un valor.

---


# Lleis dels aplicatius

Les inst√†ncies d'aplicatius han de tenir aquestes propietats:

1. Identitat:
    <br><br>
    `pure id <*> v ‚â° v`.

2. Homomorfisme:
    <br><br>
    `pure f <*> pure x ‚â° pure (f x)`.

3. Intercanvi:
    <br><br>
    `u <*> pure y ‚â° pure ($ y) <*> u`.

4. Composici√≥:
    <br><br>
    `u <*> (v <*> w) ‚â° pure (.) <*> u <*> v <*> w`.

5. Relaci√≥ amb el functor:
    <br><br>
    `fmap g x ‚â° pure g <*> x`.


---

# Instanciacions d'aplicatius

`Maybe` √©s aplicatiu:

```haskell
instance Applicative Maybe where
    pure = Just
    Nothing <*> _ = Nothing
    Just f <*> x = fmap f x
```

`Either a` √©s aplicatiu:

```haskell
instance Applicative (Either a) where
    pure = Right
    Left x <*> _ = Left x
    Right f <*> x = fmap f x
```

<br>
**Exercici:** Instancieu les llistes com a aplicatius. Hi ha dues
formes de fer-ho.

---

# Sumari

Els aplicatius permeten aplicar funcions dins d'un contenidor a objectes
dins del mateix contenidor.

- `pure` construeix un contenidor amb un valor.

- `<*>` aplica una funci√≥ dins d'un contenidor a uns valors
dins d'un contenidor:

.center[
![:scale 90%](figures/haskell/applicative_just.png)
]

Dibuixos: [adit.io](http://adit.io/posts/2013-04-17-functors,_applicatives,_and_monads_in_pictures.html)

---
class: left, middle, inverse

## Contingut

- .brown[Functors]

- .brown[Aplicatius]

- .cyan[M√≤nades]

- Entrada/sortida

- Exercicis

---

# M√≤nades

Considerem que `meitat` √©s una funci√≥ que nom√©s t√© sentit
sobre parells:

```haskell
meitat :: Int -> Maybe Int

meitat x
    | even x    = Just (div x 2)
    | otherwise = Nothing
```

.cols5050[
.col1[
.small[Podem veure la funci√≥ aix√≠: Donat un valor, retorna un valor empaquetat.]

![:scale 60%](figures/haskell/half.png)
]
.col2[
.small[Per√≤ llavors no li podem ficar valors empaquetats!]

![:scale 50%](figures/haskell/half_ouch.png)
]]

Dibuixos: [adit.io](http://adit.io/posts/2013-04-17-functors,_applicatives,_and_monads_in_pictures.html)

---

# M√≤nades

Cal una funci√≥ que
desempaqueti,
apliqui `meitat` i
deixi empaquetat.

Aquesta funci√≥ es diu `>>=`
.xs[(es llegeix *bind*)]

```haskell
Œª> Just 40 >>= meitat   üëâ Just 20
Œª> Just 31 >>= meitat   üëâ Nothing
Œª> Nothing >>= meitat   üëâ Nothing

Œª> Just 20 >>= meitat >>= meitat             üëâ Just 5
Œª> Just 20 >>= meitat >>= meitat >>= meitat  üëâ Nothing
```

L'operador `>>=` √©s una operaci√≥ de la classe `Monad`:

```haskell
class Applicative m => Monad m where
    (>>=) :: m a -> (a -> m b) -> m b
    -- i m√©s coses
```

El tipus `Maybe` √©s inst√†ncia de `Monad`:

```haskell
instance Monad Maybe where
    Nothing >>= f   =   Nothing
    Just x  >>= f   =   f x
```

---

# M√≤nades

De fet, les m√≤nades tenen tres operacions:

```haskell
class Monad m where
    return :: a -> m a
    (>>=)  :: m a -> (a -> m b) -> m b
    (>>)   :: m a -> m b -> m b

    r >> k   =   r >>= (\_ -> k)
```

- `return` empaqueta.
- `>>=` desempaqueta, aplica i empaqueta.
- `>>` √©s purament est√®tica.

---

# Instanciacions

Els tipus `Maybe`, `Either a` i `[]` s√≥n inst√†ncies de `Monad`:

```haskell
instance Monad Maybe where
    return          =   Just
    Nothing >>= f   =   Nothing
    Just x  >>= f   =   f x

instance Monad (Either a) where
    return          =   Right
    Left x  >>= f   =   Left x
    Right x >>= f   =   f x

instance Monad [] where
    return x        =   [x]
    xs >>= f        =   concatMap f xs
```

---

# Lleis de les m√≤nades

Les inst√†ncies de m√≤nades han de tenir aquestes propietats:

1. Identitat per l'esquerra:

    `return x >>= f ‚â° f x`.

2. Identitat per la dreta:

    `m >>= return ‚â° m`.

3. Associativitat:

    `(m >>= f) >>= g ‚â° m >>= (\x -> f x >>= g`).

<br>
**Nota:**
Haskell no verifica aquestes propietats (per√≤ les pot utilitzar), √©s
responsabilitat del programador fer-ho.

<br>
**Exercici:** Comproveu que `Maybe`, `Either a` i `[]` compleixen
les lleis de les m√≤nades.

---

# Notaci√≥ `do`

La **notaci√≥ `do`** √©s sucre sint√†ctic
per facilitar l'√∫s de les m√≤nades.
<br>‚áí Amb `do`, codi funcional *sembla* codi imperatiu
amb assignacions.

.cols5050[
.col1[
Els c√≤mputs es poden **seq√ºenciar**:

```haskell
do { e1 ; e2 }
```
.center[‚â°]
```haskell
do
    e1
    e2
```
.center[‚â°]
```python
e1 >> e2
```
.center[‚â°]
```python
e1 >>= \_ -> e2
```
]
.col2[
I amb `<-` **extreure** el seus resultats:

```haskell
do { x <- e1 ; e2 }
```
.center[‚â°]
```haskell
do
    x <- e1
    e2
```
.center[‚â°]
```python
e1 >>= \x -> e2
```
]]

---

# Notaci√≥ `do`: Exemple

Tenim llistes associatives amb informaci√≥ sobre propietaris de cotxes, les
seves matr√≠cules, els seus models i les seves etiquetes d'emissions:

.small[
```haskell
data Model = Seat127 | TeslaS3 | NissanLeaf | ToyotaHybrid deriving (Eq, Show)

data Etiqueta = Eco | B | C | Cap deriving (Eq, Show)

matricules = [("Joan", 6524), ("Pere", 6332), ("Anna", 5313), ("Laia", 9999)]

models = [(6524, NissanLeaf), (6332, Seat127), (5313, TeslaS3), (7572, ToyotaHybrid)]

etiquetes = [(Seat127, Cap), (TeslaS3, Eco), (NissanLeaf, Eco), (ToyotaHybrid, B)]
```
]

Donat un nom de propietari, volem saber quina √©s la seva etiqueta
d'emissions:

```haskell
etiqueta :: String -> Maybe Etiqueta
```

√âs `Maybe` perqu√®, potser el propietari no existeix, o no tenim
la seva matr√≠cula, o no tenim el seu model, o no tenim la seva etiqueta...

---

# Notaci√≥ `do`: Exemple


Ens anir√† b√© usar aquesta funci√≥ predefinida de cerca:
```haskell
lookup :: Eq a => a -> [(a, b)] -> Maybe b
```

Soluci√≥ amb `case`: üí©
```Haskell
etiqueta nom =
    case lookup nom matricules of
        Nothing  -> Nothing
        Just mat -> case lookup mat models of
                        Nothing  -> Nothing
                        Just mod -> lookup mod etiquetes
```

Soluci√≥ amb notaci√≥ `do`: üíú
```Haskell
etiqueta nom = do
    mat <- lookup nom matricules
    mod <- lookup mat models
    lookup mod etiquetes

```

---

# Notaci√≥ `do`: Exemple

Amb notaci√≥ `do`:
```Haskell
etiqueta nom = do
    mat <- lookup nom matricules
    mod <- lookup mat models
    lookup mod etiquetes

```

Transformaci√≥ de notaci√≥ `do` a funcional:
```Haskell
etiqueta nom =
    lookup nom matricules >>= \mat -> lookup mat models >>= \mod -> lookup mod etiquetes
```

Amb un format diferent queda clara l'equival√®ncia: üòú
```Haskell
etiqueta nom =
    lookup nom matricules >>= \mat ->
    lookup mat models >>= \mod ->
    lookup mod etiquetes
```

---

# Funcions predefinides per a m√≤nades

Moltes funcions predefinides tenen una extensi√≥ per la classe `Monad`:

-   `mapM`, `filterM`, `foldM`, `zipWithM`, ...

TambeÃÅ disposem d‚Äôoperacions per extendre (*lift*) operacions
per treballar amb elements de la classe Monad. S'han d'importar:

```haskell
import Control.Monad
liftM  :: Monad m => (a -> b) -> m a -> m b
liftM2 :: Monad m => (a -> b -> c) -> m a -> m b -> m c
```

Per exemple, podem crear una funci√≥ per suma `Maybe`s:

```haskell
sumaMaybes :: Num a => Maybe a -> Maybe a -> Maybe a
sumaMaybes = liftM2 (+)

Œª> sumaMaybes (Just 3) (Just 4)  üëâ Just 7
```

O fer-ho directament:

```haskell
Œª> liftM2 (+) (Just 3) (Just 4)  üëâ Just 7
```

---

# Sumari (1)


- Les m√≤nades permeten aplicar una funci√≥ que retorna un valor en un contenidor
a un valor en un contenidor.

.center[
![:scale 80%](figures/haskell/bind_def.png)
]
Dibuixos: [adit.io](http://adit.io/posts/2013-04-17-functors,_applicatives,_and_monads_in_pictures.html)

- Molts tipus predefinits s√≥n inst√†ncies de m√≤nades.

- La notaci√≥ `do` simplifica l'√∫s de les m√≤nades.

---

# Sumari (2)

- Aplicacions:

  - IO
  - Parsers
  - Logging
  - Estat mutable
  - No determinisme
  - Paral¬∑lelisme

- Lectura recomanada: [Monads for functional programming](https://homepages.inf.ed.ac.uk/wadler/papers/marktoberdorf/baastad.pdf) de P. Wadler.

---
class: left, middle, inverse

## Contingut

- .brown[Functors]

- .brown[Aplicatius]

- .brown[M√≤nades]

- .cyan[Entrada/sortida]

- Exercicis

---

# Entrada/Sortida

L'entrada/sortida en Haskell es basa en una m√≤nada:

- El programa principal √©s `main :: IO ()`

- S'usa el constructor de tipus `IO` per gestionar l'entrada/sortida.

- `IO` eÃÅs instaÃÄncia de `Monad`.

- Es sol usar amb notacioÃÅ `do`.


Algunes operacions baÃÄsiques:

```haskell
getChar     :: IO Char              -- obt√© seg√ºent car√†cter
getLine     :: IO String            -- obt√© seg√ºent l√≠nia
getContents :: IO String            -- obt√© tota l'entrada

putChar     :: Char -> IO ()        -- escriu un car√†cter
putStr      :: String -> IO ()      -- escriu un text
putStrLn    :: String -> IO ()      -- escriu un text i un salt de l√≠nia
print       :: Show a => a -> IO () -- escriu qualsevol showable
```

`()` √©s una tupla de zero camps
i `()` √©s l'√∫nic valor de tipus `()`.
<br>
(‚áî `void` de C).

---

#  Hello world!

```haskell
main = do
    putStrLn "Com et dius?"
    nom <- getLine
    putStrLn $ "Hola " ++ nom ++ "!"
```

Compilaci√≥ i execuci√≥:

```bash
*> ghc programa.hs
[1 of 1] Compiling Main             ( programa.hs, programa.o )
Linking programa ...

*> ./programa
Com et dius?
*Jordi
Hola Jordi!
```

---

#  Del rev√©s

```haskell
main = do
    x <- getLine
    let y = reverse x
    putStrLn x
    putStrLn y
```

Compilaci√≥ i execuci√≥:

```bash
*> ghc programa.hs
[1 of 1] Compiling Main             ( programa.hs, programa.o )
Linking programa ...

*> ./programa
*GAT
GAT
TAG
```

---

#  Exemple

Llegir seq√º√®ncia de l√≠nies acabades en `*`
i escriure cadascuna del rev√©s:

```haskell
main = do
    line <- getLine
    if line /= "*" then do
        putStrLn $ reverse line
        main
    else
        return ()
```

---

#  Exemple


Llegir seq√º√®ncia de l√≠nies
i escriure cadascuna del rev√©s:

```haskell
main = do
    contents <- getContents
    mapM_ (putStrLn . reverse) (lines contents)
```

L'E/S tamb√© √©s *lazy*, no cal preocupar-se perqu√® l'entrada
sigui massa llarga.

---

# `where` en  notaci√≥ `do`

Degut a la definicioÃÅ del `>>=`, el `where` pot donar problemes:

```haskell
main = do
    x <- getLine
    print f
        where f = factorial (read x)

    ‚ùå error: Variable not in scope: x :: String
```

Si ho escrivim amb `>>=`, tenim

```haskell
main = getLine >>= \x -> print f
    where f = factorial (read x)
```

que no pot ser, ja que a les definicions del `where` no podem usar la
variable abstreta `x`.

---

# `let` en  notaci√≥ `do`

Amb el `do` cal usar el `let` (sense `in`):

```haskell
main = do
    x <- getLine
    let f = factorial (read x)
    print f
```

Alternativament (meÃÅs lleig):

```haskell
main = do
    x <- getLine
    f <- return $ factorial (read x)
    print f
```

---

# Intu√Øci√≥ sobre la m√≤nada `IO`

Entrada/sortida com funcions
que modifiquen el m√≥n: `m√≥n1 ‚üø m√≥n2`.

Cadascuna s'encadena amb l'anterior, com un relleu.
![:scale 3%](figures/haskell/baton-relay.png)

**Exemple:** Llegir i escriure dos car√†cters.

.cols5050[
.col1[
```haskell
data World = ... -- descripci√≥ del m√≥n

myGetChar :: World -> (World, Char)

myPutChar :: Char -> World -> (World, ())

myMain :: World -> (World, ())

myMain w0 = let (w1, c1) = myGetChar w0
                (w2, c2) = myGetChar w1
                (w3, ()) = myPutChar c1 w2
                (w4, ()) = myPutChar c2 w3
            in  (w4, ())
```
(1) Passant el relleu.
]
.col2[
]
]

---

# Intu√Øci√≥ sobre la m√≤nada `IO`

Entrada/sortida com funcions
que modifiquen el m√≥n: `m√≥n1 ‚üø m√≥n2`.

Cadascuna s'encadena amb l'anterior, com un relleu.
![:scale 3%](figures/haskell/baton-relay.png)

**Exemple:** Llegir i escriure dos car√†cters.

.cols5050[
.col1[
```haskell
data World = ... -- descripci√≥ del m√≥n

myGetChar :: World -> (World, Char)

myPutChar :: Char -> World -> (World, ())

myMain :: World -> (World, ())

myMain w0 = let (w1, c1) = myGetChar w0
                (w2, c2) = myGetChar w1
                (w3, ()) = myPutChar c1 w2
                (w4, ()) = myPutChar c2 w3
            in  (w4, ())
```
(1) Passant el relleu.
]
.col2[
```haskell
type IO a = World -> (World, a)

getChar :: IO Char

putChar :: Char -> IO ()

main :: IO ()

main =
    getChar >>= \c1 ->
    getChar >>= \c2 ->
    putChar c1 >>
    putChar c2
```
(2) Fent que `IO` sigui inst√†ncia de `Monad`.
]
]

---

# Intu√Øci√≥ sobre la m√≤nada `IO`

Entrada/sortida com funcions
que modifiquen el m√≥n: `m√≥n1 ‚üø m√≥n2`.

Cadascuna s'encadena amb l'anterior, com un relleu.
![:scale 3%](figures/haskell/baton-relay.png)

**Exemple:** Llegir i escriure dos car√†cters.


.cols5050[
.col1[
```haskell
type IO a = World -> (World -> a)

getChar :: IO Char

putChar :: Char -> IO ()

main :: IO ()

main =
    getChar >>= \c1 ->
    getChar >>= \c2 ->
    putChar c1 >>
    putChar c2
```
(2) Fent que `IO` sigui inst√†ncia de `Monad`.
]
.col2[
```haskell








main = do
    c1 <- getChar
    c2 <- getChar
    putChar c1
    putChar c2
```
(3) Usant notaci√≥ `do`.
]
]

---

#  Sumari

- Hem vist tres classes predefinides molt importants en Haskell:
    Functors, Aplicatius, M√≤nades.

.center[
![:scale 90%](figures/haskell/monads_recap.png)
]

- Molts tipus predefinits s√≥n inst√†ncies d'aquestes classes:
    `Maybe`, `Either`, llistes, tuples, funcions, `IO`, ...

- La notaci√≥ `do` simplifica l'√∫s de les m√≤nades.

- La classe `IO` permet disposar d'entrada/sortida en un llenguatge
funcional pur.

---

# Final

L'**estat d'un programa** descriu tota la informaci√≥
que no √©s local a una funci√≥ en particular. Aix√≤ inclou:

- variables globals
- entrada
- sortida

Pensar sobre un programa amb estat √©s dif√≠cil perqu√®:

- L'estat perviu d'una crida d'una funci√≥ a una altra.
- L'estat √©s a l'abast de totes les funcions.
- L'estat √©s mutable.
- L'estat canvia en el temps.
- Cap funci√≥ √©s responsable de l'estat.

Estat: üí©
<br>Sense estat: üíú

Les m√≤nades no eliminen la noci√≥ d'estat en un programa,
per√≤ eliminen la necessitat de mencionar-lo.

---
class: left, middle, inverse

## Contingut

- .brown[Functors]

- .brown[Aplicatius]

- .brown[M√≤nades]

- .brown[Entrada/sortida]

- .cyan[Exercicis]

---

# Exercicis

Feu aquests problemes de Jutge.org:

- Functors, aplicatius i m√≤nades:

    - [P70540](https://jutge.org/problems/P70540) Expressions
    - [P50086](https://jutge.org/problems/P50086) Cua 2
    - [P58738](https://jutge.org/problems/P58738) Arbres amb talla

- Entrada/sortida:

    - [P87974](https://jutge.org/problems/P87974) Hola / Ad√©u
    - [P87082](https://jutge.org/problems/P87082) √çndex massa corporal
