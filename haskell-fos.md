class: center, middle

### Llenguatges de Programaci√≥

## Sessi√≥ 2: funcions d'ordre superior

![:scale 40%](figures/haskell.png)<br><br>

**Jordi Petit**

![:scale 75%](figures/fib.png)

---
class: left, middle, inverse

## Contingut

- .cyan[Funcions d'ordre superior]

- Funcions d'ordre superior habituals

- Aplicacions

- Exercicis

---

# Funcions d'ordre superior

Una **funci√≥ d'ordre superior** (FOS) √©s una funci√≥ que rep o retorna
funcions.

Punt clau: les funcions soÃÅn objectes de primera classe.

<br>
**Exemple en C++:**

```c++
bool compare(int x, int y) {
    return x > y;
}

int main() {
    vector<int> v = { ... };
    sort(v.begin(), v.end(), compare);      // sort √©s funci√≥ d'ordre superior
}
```

---

# Funcions d'ordre superior

**Exemples:** 

La funci√≥ predefinida `map` aplica una funci√≥ a cada element d'una llista.

```haskell
map :: (a -> b) -> [a] -> [b]
map f [] = []
map f (x:xs) = f x : map f xs
```

```haskell
Œª> map odd [1..5]
üëâ [True, False, True, False, True]
```

La funci√≥ predefinida `(.)` retorna la composici√≥ de dues funcions:

```haskell
(.) :: (b -> c) -> (a -> b) -> (a -> c)
(f . g) x = f (g x)
```

```haskell
Œª> (reverse . sort) [5, 3, 5, 2]
üëâ [5, 5, 3, 2]
```

---

# Funcions d'ordre superior


**Exemple:** La funci√≥ `apli2` aplica dos cops una funci√≥ a un element.

```haskell
apli2 :: (a -> a) -> a -> a
apli2 f x = f (f x)
```

```haskell
Œª> apli2 sqrt 16.0
üëâ 2.0
```

De forma equivalent:

```haskell
apli2 :: (a -> a) -> (a -> a)
apli2 f = f . f
```

```haskell
Œª> apli2 sqrt 16.0
üëâ 2.0
```

Petit exercici:

```haskell
Œª> per2 x = 2 * x
Œª> apli2 (apli2 per2) 2
üëâ ?
```

---

# Funcions an√≤nimes

Les funcions an√≤nimes (funcions Œª) s√≥n expressions que representen
una funci√≥ sense nom.

```haskell
\x -> x + 3         -- defineix funci√≥ an√≤nima que, donada una x, retorna x + 3
                    -- si proveu d'escriure-la, Haskell s'enfada perqu√® no ho sap fer
(\x -> x + 3) 4     -- aplica la funci√≥ an√≤nima sobre 4
üëâ 7
```

Funci√≥ amb nom:

```haskell
doble x = 2 * x                     -- equival a doble = \x -> 2 * x

Œª> doble 3                          üëâ 6
Œª> map doble [1, 2, 3]              üëâ [2, 4, 6]
```

Funci√≥ an√≤nima:

```haskell
Œª> map (\x -> 2 * x) [1, 2, 3]      üëâ [2, 4, 6]
```

**Utilitat**: quan s√≥n curtes i nom√©s s'utilitzen un cop.

Tamb√© s√≥n √∫tils per realitzar transformacions de programes.

---

# Funcions an√≤nimes

M√∫ltiples par√†metres:

```haskell
\x y -> x + y
```

√©s equivalent a

```haskell
\x -> \y -> x + y
```

que vol dir

```haskell
\x -> (\y -> x + y)
```

---

# Seccions

Les **seccions** permeten aplicar operadors infixos parcialment.

.cols5050[
.col1[
Per la dreta: <br>
`(‚¶ª y) ‚â° \x -> x ‚¶ª y`
]
.col2[
Per l'esquerra: <br>
`(y ‚¶ª) ‚â° \x -> y ‚¶ª x`
]]

Exemples:

```haskell
Œª> doble = (* 2)           -- ‚â° (2 *)
Œª> doble 3
üëâ 6

Œª> map (* 2) [1, 2, 3]    -- millor que map (\x -> x * 2) [1, 2, 3]
üëâ [2, 4, 6]

Œª> meitat = (/ 2)          -- ‚â¢ (2 /)
Œª> meitat 6
üëâ 3

Œª> √©sMaj√∫scula = (`elem` ['A'..'Z'])
Œª> √©sMaj√∫scula 'b'
üëâ False
```

---
class: left, middle, inverse

## Contingut

- .brown[Funcions d'ordre superior]

- .cyan[Funcions d'ordre superior habituals]

- Aplicacions

- Exercicis

---

# Funcions d'ordre superior habituals

Algunes funcions d'ordre superior predefined s'utilitzen molt habitualment:

- `(.)`
- `($)`
- `const`
- `id`
- `flip`
- `map`
- `filter`
- `zipWith`
- `all`, `any`
- `dropWhile`, `takeWhile`
- `iterate`,
- `foldl`, `foldr`
- `scanl`, `scanr`

---

# composici√≥ `(.)`


- Signatura:

    ```Haskell
    (.) :: (b -> c) -> (a -> b) -> a -> c
    ```

- Descripci√≥:

    `f . g` √©s la composici√≥ de les funcions `f` i `g`.

- Exemples:

    ```Haskell
    Œª> tresMesGrans = take 3 . reverse . sort

    Œª> :type tresMesGrans
    tresMesGrans :: Ord a => [a] -> [a]

    Œª> tresMesGrans [3, 1, 2, 6, 7]
    üëâ [7, 6, 3]
    ```


---

# aplicaci√≥ `($)`


- Signatura:

    ```Haskell
    ($) :: (a -> b) -> a -> b
    ```

- Descripci√≥:

    `f $ x` √©s el mateix que `f x`. Sembla in√∫til, per√≤ degut a la baixa prioritat d'aquest operador,
    ens permet ometre molts par√®ntesis de tancar!

- Exemples:

    ```Haskell
    Œª> tail (tail (tail (tail "Jordi")))
    üëâ "i"
    Œª> tail $ tail $ tail $ tail "Jordi"
    üëâ "i"
    ```

---

# const

- Signatura:

    ```Haskell
    const :: a -> b -> a
    ```

- Descripci√≥:

    `const x` √©s una funci√≥ que sempre retorna `x`, independentment de qu√® se li apliqui.

- Exemples:

    ```Haskell
    Œª> map (const 42) [1 .. 5]
    üëâ [42, 42, 42, 42, 42]
    ```

---

# id

- Signatura:

    ```Haskell
    id :: a -> a
    ```

- Descripci√≥:

    `id` √©s la funci√≥ identitat. Tamb√© sembla in√∫til, pero va b√© en algun moment.

- Exemples:

    ```Haskell
    Œª> map id [1 .. 5]
    üëâ [1, 2, 3, 4, 5]
    ```

---

# flip

- Signatura:

    ```Haskell
    flip :: (a -> b -> c) -> (b -> a -> c)
    ```

- Descripci√≥:

    `flip f` retorna la funci√≥ `f` per√≤ amb els seus dos par√†metres invertits.
    Es defineix per

    ```Haskell
    flip f x y = f y x
    ```

- Exemples:

    ```Haskell
    Œª> meitat = flip div 2

    Œª> meitat 10
    üëâ 5
    ```

---

# map

- Signatura:
    ```Haskell
    map :: (a -> b) -> [a] -> [b]
    ```

- Descripci√≥:
    `map f xs` √©s la llista que s'obt√© al aplicar la funci√≥ `f`
    a cada element de la llista `xs`, de forma que
    `map f [x1, x2, ..., xn]` √©s `[f x1, f x2, ..., f xn]`.
.center[![:scale 40%](figures/haskell/map.png)]
    `[y1, y2, y3, y4] = map f [x1, x2, x3, x4]`

- Exemples:
    ```Haskell
    Œª> map even [2, 4, 6, 7]  üëâ [True, True, True, False]
    Œª> map (*2) [2, 4, 6, 7]  üëâ [4, 8, 12, 14]
    ```
---

# filter

- Signatura:

    ```Haskell
    filter :: (a -> Bool) -> [a] -> [a]
    ```

- Descripci√≥:

    `filter p xs` √©s la subllista dels elements de `xs` que compleixen el
    predicat `p`.

    (Un **predicat** √©s una funci√≥ que retorna un Boole√†.)


- Exemples:

    ```Haskell
    Œª> filter even [2, 1, 4, 6, 7]
    üëâ [2, 4, 6]
    ```

---

# zipWith

- Signatura:
    ```Haskell
    zipWith :: (a -> b -> c) -> [a] -> [b] -> [c]
    ```

- Descripci√≥:
    `zipWith op xs ys` √©s la llista obtinguda operant cada element de `xs`
    amb cada element de `ys` via la funci√≥ `op`, d'esquerra a dreta, mentre n'hi hagi.
.center[![:scale 40%](figures/haskell/zipWith.png)]
    `[z1, z2, z3, z4] = zipWith f [x1, x2, x3, x4, x5] [y1, y2, y3, y4]`

- Exemples:
    ```Haskell
    Œª> zipWith (+) [1, 2, 3] [5, 1, 8, 9]
    üëâ [6, 3, 11]
    ```

---

# all

- Signatura:

    ```Haskell
    all :: (a -> Bool) -> [a] -> Bool
    ```

- Descripci√≥:

    `all p xs` indica si tots els elements de `xs` compleixen el predicat `p`.


- Exemples:

    ```Haskell
    Œª> all even [2, 1, 4, 6, 7]
    üëâ False
    Œª> all even [2, 4, 6]
    üëâ True
    ```

---

# any

- Signatura:

    ```Haskell
    any :: (a -> Bool) -> [a] -> Bool
    ```

- Descripci√≥:

    `any p xs` indica si algun dels elements de `xs` compleix el predicat `p`.


- Exemples:

    ```Haskell
    Œª> any even [2, 1, 4, 6, 7]
    üëâ True
    Œª> any odd [2, 4, 6]
    üëâ False
    ```

---

# dropWhile

- Signatura:

    ```Haskell
    dropWhile :: (a -> Bool) -> [a] -> [a]
    ```

- Descripci√≥:

    `dropWhile p xs` √©s la subllista de `xs` que elimina els primers elements de `xs`
    que compleixen el predicat `p` (fins al final o al primer que no la compleix).


- Exemples:

    ```Haskell
    Œª> dropWhile even [2, 4, 6, 7, 8]
    üëâ [7, 8]
    Œª> dropWhile even [2, 4]
    üëâ []
    ```

---

# takeWhile

- Signatura:

    ```Haskell
    takeWhile :: (a -> Bool) -> [a] -> [a]
    ```

- Descripci√≥:

    `takeWhile p xs` √©s la subllista de `xs` que cont√© els primers elements de `xs` que compleixen el predicat `p` (fins al final o al primer que no la compleix).


- Exemples:

    ```Haskell
    Œª> takeWhile even [2, 4, 6, 7, 8]
    üëâ [2, 4, 6]
    Œª> takeWhile even [1, 3]
    üëâ []
    ```

---

# iterate

- Signatura:

    ```Haskell
    iterate :: (a -> a) -> a -> [a]
    ```

- Descripci√≥:

    `iterate f x` retorna la llista infinita `[x, f x, f (f x), f (f (f x)), ...]`.

    <div id='cy_iterate' style='width: 20em; height: 6em;'></div>

    `ys = iterate f x`


- Exemples:

    ```Haskell
    Œª> iterate (*2) 1
    üëâ [1, 2, 4, 8, 16, ...]
    ```

---

# foldl

- Signatura:
    ```Haskell
    foldl :: (b -> a -> b) -> b -> [a] -> b
    ```

- Descripci√≥:
    `foldl ‚äï x0 xs` desplega un operador ‚äï per l'esquerra, de forma que
    `foldl ‚äï x0 [x1, x2, ..., xn]` √©s `(((x0 ‚äï x1) ‚äï x2) ‚äï ...) ‚äï xn`.
.center[![:scale 60%](figures/haskell/foldl.png)]
    `y = foldl f x0 [x1, x2, x3, x4]`

- Exemples:
    ```Haskell
    Œª> foldl (+) 0 [3, 2, (-1)]             -- (((0 + 3) + 2) + (-1))
    üëâ 4
    ```

---

# foldr

- Signatura:

    ```Haskell
    foldr :: (a -> b -> b) -> b -> [a] -> b
    ```

- Descripci√≥:
    `foldr ‚äï x0 xs` desplega un operador per la dreta, de forma que
    `foldr ‚äï x0 [x1, x2, ..., xn]` √©s `x1 ‚äï (x2 ... ‚äï (xn ‚äï x0)))`.
.center[![:scale 60%](figures/haskell/foldr.png)]
    `y = foldr f x0 [x1, x2, x3, x4]`

- Exemples:

    ```Haskell
    Œª> foldr (+) 0 [3, 2, (-1)]             -- 3 + ((2 + ((-1) + 0)))
    üëâ 4
    ```

---

# scanl

- Signatura:

    ```Haskell
    scanl :: (b -> a -> b) -> b -> [a] -> [b]
    ```

- Descripci√≥:
    `scanl f x0 xs` √©s com `foldl f x0 xs` per√≤ enlloc de retornar el valor final, retorna la llista amb tots els resultats intermigs.
.center[![:scale 50%](figures/haskell/scanl.png)]
    `[y0, y1, y2, y3, y4] = scanl f x0 [x1, x2, x3, x4]`

- Exemples:

    ```Haskell
    Œª> scanl (+) 0 [3, 2, (-1)]
    üëâ [0, 3, 5, 4]
    ```

---

# scanr

- Signatura:

    ```Haskell
    scanr :: (a -> b -> b) -> b -> [a] -> [b]
    ```

- Descripci√≥:
    `scanr f x0 xs` √©s com `foldr f x0 xs` per√≤ enlloc de retornar el valor final, retorna la llista amb tots els resultats intermigs.
.center[![:scale 50%](figures/haskell/scanl.png)]
    `[y0, y1, y2, y3, y4] = scanr f x0 [x1, x2, x3, x4]`

- Exemples:

    ```Haskell
    Œª> scanr (+) 0 [3, 2, (-1)]
    üëâ [4, 1, -1, 0]
    ```
---

# Perspectiva

## map

- C++

    ```c++
    vector<X> xs = { ... };

    vector<Y> ys;
    for (int i = 0; i < xs.size(); ++i) {
        ys.push_back(func(xs[i]));
    }
    ```

- Haskell

    ```haskell
    ys = map func xs
    ```
---

# Perspectiva

## filter

- C++

    ```c++
    vector<X> xs = { ... };

    vector<X> ys;
    for (int i = 0; i < xs.size(); ++i) {
        if (pred(xs[i])) {
            ys.push_back(xs[i]);
        }
    }
    ```

- Haskell

    ```haskell
    ys = filter pred xs
    ```
---

# Perspectiva

## foldl

- C++

    ```c++
    vector<X> xs = { ... };

    Y y = zero;
    for (int i = 0; i < xs.size(); ++i) {
        y = oper(y, xs[i]);
    }
    ```

- Haskell

    ```haskell
    y = foldl oper zero xs
    ```
---

# Perspectiva

## composici√≥

- Haskell

    ```haskell
    (take 3 . reverse . sort) dades
    ```

- Shell

    ```bash
    cat dades | sort | tac | head -3
    ```

---
class: left, middle, inverse

## Contingut

- .brown[Funcions d'ordre superior]

- .brown[Funcions d'ordre superior habituals]

- .cyan[Aplicacions]

- Exercicis

---

# Diccionaris amb FOSs

Volem definir un Diccionari de Strings a Ints amb
valors per defecte
usant funcions d'ordre superior.

## Interf√≠cie

```haskell
type Dict = (String -> Int)     -- Defineix un tipus sin√≤nim a la typedef

create :: Int -> Dict
search :: Dict -> String -> Int
insert :: Dict -> String -> Int -> Dict
```

---

# Diccionaris

Volem definir un TAD Diccionari de Strings a Ints amb
valors per defecte
usant funcions d'ordre superior.

**Interf√≠cie**

```haskell
type Dict = (String -> Int)     -- Defineix un tipus sin√≤nim a la typedef

create :: Int -> Dict
search :: Dict -> String -> Int
insert :: Dict -> String -> Int -> Dict
```

.cols5050[
.col1[
**Primera versi√≥**

```haskell
type Dict = (String -> Int)

create def = \key -> def

search dict key = dict key

insert dict key value = \x ->
    if key == x then value
    else search dict x
```
]
.col2[
**Segona versi√≥**

```haskell
type Dict = (String -> Int)

create = const

search = ($)

insert dict key value x
    | key == x      = value
    | otherwise     = dict x

```
]]

---

# Dividir i v√®ncer

Funci√≥ d'ordre superior gen√®rica `dIv` per
l'esquema de dividir i v√®ncer.

**Interf√≠cie**

```haskell
dIv :: (a -> Bool) -> (a -> b) -> (a -> (a, a)) -> (a -> (a, a) -> (b, b) -> b) -> a -> b
```

on `a` √©s el tipus del problema, `b` √©s el tipus de la soluci√≥, i
<br>`dIv trivial directe dividir v√®n√ßer x` utilitza:


- `trivial :: a -> Bool` per saber si un problema √©s trivial.

- `directe :: a -> b` per solucionar directament un problema trivial.

- `dividir :: a -> (a, a)` per dividir un problema no trivial en un parell de subproblemes m√©s petits.

- `v√®n√ßer :: a -> (a, a) -> (b, b) -> b` per, donat un problema no trivial, els seus subproblemes i les seves respectives subsolucions, obtenir la soluci√≥ al problema original.

- `x :: a` denota el problema a solucionar.

---

# Dividir i v√®ncer

**Soluci√≥**

```haskell
dIv :: (a -> Bool) -> (a -> b) -> (a -> (a, a)) -> (a -> (a, a) -> (b, b) -> b) -> a -> b

dIv trivial directe dividir v√®n√ßer x
    | trivial x     = directe x
    | otherwise     = v√®n√ßer x (x1, x2) (y1, y2)
                          where
                              (x1, x2) = dividir x
                              y1 = dIv trivial directe dividir v√®n√ßer x1
                              y2 = dIv trivial directe dividir v√®n√ßer x2

```

---

# Dividir i v√®ncer

**Soluci√≥ capturant el context**

```haskell
dIv :: (a -> Bool) -> (a -> b) -> (a -> (a, a)) -> (a -> (a, a) -> (b, b) -> b) -> a -> b

dIv trivial directe dividir v√®n√ßer = dIv'
    where
        dIv' x
            | trivial x = directe x
            | otherwise = v√®n√ßer x (x1, x2) (y1, y2)
                              where
                                  (x1, x2) = dividir x
                                  y1 = dIv' x1
                                  y2 = dIv' x2
```



---

# Dividir i v√®ncer

**Implementaci√≥ de Quicksort amb Dividir i v√®ncer**

```haskell
qs :: Ord a => [a] -> [a]

qs = dIv trivial directe dividir v√®ncer
  where
    trivial []   = True
    trivial [_]  = True
    trivial _    = False

    directe = id

    dividir (x:xs) = (menors, majors)
      where menors = filter (<= x) xs
        majors = filter (>  x) xs

    dividir' (x:xs) = partition (<= x) xs   -- equivalent amb funci√≥ predefinida

    v√®ncer (x:_) _ (ys1, ys2) = ys1 ++ [x] ++ ys2
```

---
class: left, middle, inverse

## Contingut

- .brown[Funcions d'ordre superior]

- .brown[Funcions d'ordre superior habituals]

- .brown[Aplicacions]

- .cyan[Exercicis]

---

# Exercicis

1. Feu aquests problemes de Jutge.org:

    - [P93632](https://jutge.org/problems/P93632) Usage of higher-order functions (1)
    - [P31745](https://jutge.org/problems/P31745) Usage of higher order functions (2)
    - [P90677](https://jutge.org/problems/P90677) Definition of higher-order functions (1)
    - [P71775](https://jutge.org/problems/P71775) Definition of higher-order functions (2)

1. Re-implementeu les funcions habituals sobre llistes.

    - Useu `myLength` enlloc de `length` per evitar xocs de noms.
    - No useu recursivitat: useu funcions d'ordre superior.

1. Busqueu a [HoogŒªe](https://www.haskell.org/hoogle/) informaci√≥ sobre aquestes funcions:

    - `foldl1`, `foldr1`, `scanl1`, `scanr1`
    - `partition`
    - `concatMap`
    - `zipWith3`
    - `mapAccumL`, `mapAccumR`
